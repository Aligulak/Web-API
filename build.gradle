plugins {
    id "java"
    id "com.qixalite.spongestart" version "1.6.0"
    id "ninja.miserable.blossom" version "1.0.1"
    id "com.github.johnrengelman.shadow" version "1.2.4"
}

group = project.group;
version = "${project.version}-S${project.spongeVersion}";

repositories {
    mavenCentral()
    maven {
        url "https://repo.spongepowered.org/maven/"
    }
}

blossom {
    def location = 'src/main/java/valandur/webapi/WebAPI.java'
    replaceToken '@version@', project.version, location
}

spongestart {
    eula true
    minecraft project.minecraftVersion
}

dependencies {
    compileOnly group: "org.spongepowered", name: "spongeapi", version: "${project.spongeVersion}.0-SNAPSHOT"
    compile group: "org.eclipse.jetty", name: "jetty-server", version: "9.4.2.v20170220"
    compile group: "org.eclipse.jetty", name: "jetty-servlet", version: "9.4.2.v20170220"
    compile group: "org.eclipse.jetty", name: "jetty-http", version: "9.4.2.v20170220"
    compile group: "com.fasterxml.jackson.core", name: "jackson-core", version: "2.8.7"
    compile group: "com.fasterxml.jackson.core", name: "jackson-databind", version: "2.8.7"
    compile group: "com.fasterxml.jackson.core", name: "jackson-annotations", version: "2.8.7"
    compile group: "org.reflections", name: "reflections", version: "0.9.10"
}

jar {
    enabled = false
}

shadowJar {
    configurations  = [project.configurations.compile]

    relocate "org.eclipse.jetty", "valandur.webapi.shadow.org.eclipse.jetty"
    relocate "com.fasterxml.jackson", "valandur.webapi.shadow.fasterxml.jackson"
    relocate "javax.servlet", "valandur.webapi.shadow.javax.servlet"
    relocate "org.reflections", "valandur.webapi.shadow.org.reflections"

    exclude "/about.html"
    exclude "/jetty-dir.css"
    exclude "/StartServer.class"
    exclude "/StartServer.java"
    exclude '/StartServer$SpongeClassLoader.class'

    archiveName = "webapi-${version}.jar"
}

artifacts {
    archives shadowJar
}
