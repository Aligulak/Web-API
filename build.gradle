plugins {
    id "java"
    id "com.qixalite.spongestart" version "1.6.2"
    id "ninja.miserable.blossom" version "1.0.1"
    id "com.github.johnrengelman.shadow" version "1.2.4"
}

group = "valandur.webapi";

allprojects {
    apply plugin: "java"

    version = "${project.version}-S${project.spongeVersion}";

    repositories {
        mavenCentral()
        maven {
            url "https://repo.spongepowered.org/maven/"
        }
        maven {
            url "http://repo.drnaylor.co.uk/artifactory/list/minecraft"
        }
        maven {
            url "http://repo.bstats.org/content/repositories/releases/"
        }
    }

    dependencies {
        compileOnly group: "org.spongepowered", name: "spongeapi", version: "${project.spongeVersion}.0-SNAPSHOT"
        compile group: "io.sentry", name: "sentry", version: "1.4.0"
        compile group: "org.bstats", name: "bstats-sponge", version: "1.2"
        compile group: "org.eclipse.jetty", name: "jetty-server", version: "9.4.2.v20170220"
        compile group: "org.eclipse.jetty", name: "jetty-servlet", version: "9.4.2.v20170220"
        compile group: "org.eclipse.jetty", name: "jetty-http", version: "9.4.2.v20170220"
        compile group: "com.fasterxml.jackson.core", name: "jackson-core", version: "2.8.7"
        compile group: "com.fasterxml.jackson.core", name: "jackson-databind", version: "2.8.7"
        compile group: "com.fasterxml.jackson.core", name: "jackson-annotations", version: "2.8.7"
    }
}

blossom {
    def locMain = "src/main/java/valandur/webapi/WebAPI.java"
    replaceToken "@version@", project.version, locMain
}

spongestart {
    eula true
    minecraft project.minecraftVersion

    spongeForgeVersion "1.11.2-2393-6.1.0-BETA-2556"
    spongeVanillaVersion "1.11.2-6.1.0-BETA-17"
}

dependencies {
    compile project(":webapi-api")
    compile group: "org.reflections", name: "reflections", version: "0.9.10"
    compile group: "org.mindrot", name: "jbcrypt", version: "0.4"

    compileOnly group: "io.github.nucleuspowered", name: "nucleus-api", version: "1.1.0-SNAPSHOT-S6.0"
    compileOnly fileTree(dir: "lib", include: "*.jar")
}

shadowJar {
    configurations  = [project.configurations.compile]

    relocate "io.sentry", "valandur.webapi.shadow.io.sentry"
    relocate "org.bstats", "valandur.webapi.shadow.org.bstats"
    relocate "com.fasterxml", "valandur.webapi.shadow.com.fasterxml"
    relocate "edu.umd", "valandur.webapi.shadow.edu.umd"
    relocate "javassist", "valandur.webapi.shadow.javassist"
    relocate "javax.servlet", "valandur.webapi.shadow.javax.servlet"
    relocate "org.mindrot", "valandur.webapi.shadow.org.mindrot"
    relocate "org.eclipse", "valandur.webapi.shadow.org.eclipse"
    relocate "org.reflections", "valandur.webapi.shadow.org.reflections"
    relocate "net.jodah", "valandur.webapi.shadow.net.jodah"
    relocate "net.jcip", "valandur.webapi.shadow.net.jcip"

    exclude "/about.html"
    exclude "/jetty-dir.css"
    exclude "/valandur/webapi/api/WebAPIMod.class"

    archiveName = "webapi-${version}.jar"
}
shadowJar.dependsOn([':webapi-api:build'])

task copyJars(type: Copy, dependsOn: ":webapi-api:build") {
    from([subprojects.jar, shadowJar])
    into project.file("artifacts")
}

artifacts {
    archives shadowJar
}

build.dependsOn(shadowJar)
build.dependsOn(copyJars)
