plugins {
    id "java"
    id "com.qixalite.spongestart" version "1.6.2"
    id "ninja.miserable.blossom" version "1.0.1"
    id "com.github.johnrengelman.shadow" version "1.2.4"
}

group = "valandur.webapi";

allprojects {
    apply plugin: "java"

    version = "${project.version}-S${project.spongeVersion}";

    repositories {
        mavenCentral()
        maven {
            url "https://repo.spongepowered.org/maven/"
        }
        maven {
            url "http://repo.bstats.org/content/repositories/releases/"
        }
    }

    dependencies {
        compileOnly group: "org.spongepowered", name: "spongeapi", version: "${project.spongeVersion}.0-SNAPSHOT"
        compile group: "io.sentry", name: "sentry", version: "1.5.3"
        compile group: "org.bstats", name: "bstats-sponge", version: "1.2"
        compile group: "org.eclipse.jetty", name: "jetty-server", version: "9.4.6.v20170531"
        compile group: "org.eclipse.jetty", name: "jetty-servlet", version: "9.4.6.v20170531"
        compile group: "org.eclipse.jetty", name: "jetty-http", version: "9.4.6.v20170531"
        compile group: "org.eclipse.jetty", name: "jetty-rewrite", version: "9.4.6.v20170531"
        compile group: "com.fasterxml.jackson.core", name: "jackson-core", version: "2.9.0"
        compile group: "com.fasterxml.jackson.core", name: "jackson-databind", version: "2.9.0"
        compile group: "com.fasterxml.jackson.core", name: "jackson-annotations", version: "2.9.0"
    }
}

blossom {
    def locMain = "src/main/java/valandur/webapi/WebAPI.java"
    replaceToken "@version@", project.version, locMain
}

spongestart {
    eula true
    minecraft project.minecraftVersion
}

dependencies {
    compile project(":webapi-api")
    compile group: "org.mindrot", name: "jbcrypt", version: "0.4"

    compileOnly files("lib/HuskyCrates-v1.7.2.jar")
    compileOnly files("lib/MMCTickets-1.4.0-API-5.2-7.X.jar")
    compileOnly files("lib/Nations-2.8-S6.1-MC1.11.2.jar")
    compileOnly files("lib/Nucleus-1.1.3-LTS-api.jar")
}

shadowJar {
    configurations  = [project.configurations.compile]

    relocate "io.sentry", "valandur.webapi.shadow.io.sentry"
    relocate "org.bstats", "valandur.webapi.shadow.org.bstats"
    relocate "com.fasterxml", "valandur.webapi.shadow.com.fasterxml"
    relocate "edu.umd", "valandur.webapi.shadow.edu.umd"
    relocate "javassist", "valandur.webapi.shadow.javassist"
    relocate "javax.servlet", "valandur.webapi.shadow.javax.servlet"
    relocate "org.mindrot", "valandur.webapi.shadow.org.mindrot"
    relocate "org.eclipse", "valandur.webapi.shadow.org.eclipse"
    relocate "net.jodah", "valandur.webapi.shadow.net.jodah"
    relocate "net.jcip", "valandur.webapi.shadow.net.jcip"

    exclude "/about.html"
    exclude "/jetty-dir.css"
    exclude "/valandur/webapi/api/WebAPIMod.class"

    archiveName = "webapi-${version}.jar"
}
shadowJar.dependsOn([':webapi-api:build'])

task copyJars(type: Copy, dependsOn: ":webapi-api:build") {
    from([subprojects.jar, shadowJar])
    into project.file("artifacts")
}

artifacts {
    archives shadowJar
}

build.dependsOn(shadowJar)
build.dependsOn(copyJars)
