plugins {
    id "java"
    id "com.qixalite.spongestart" version "1.6.0"
    id "ninja.miserable.blossom" version "1.0.1"
    id "com.github.johnrengelman.shadow" version "1.2.4"
}

group = "valandur.webapi";

allprojects {
    apply plugin: "java"

    version = "${project.version}-S${project.spongeVersion}";

    repositories {
        mavenCentral()
        maven {
            url "https://repo.spongepowered.org/maven/"
        }
    }

    dependencies {
        compileOnly group: "org.spongepowered", name: "spongeapi", version: "${project.spongeVersion}.0-SNAPSHOT"
        compile group: "org.eclipse.jetty", name: "jetty-server", version: "9.4.2.v20170220"
        compile group: "org.eclipse.jetty", name: "jetty-servlet", version: "9.4.2.v20170220"
        compile group: "org.eclipse.jetty", name: "jetty-http", version: "9.4.2.v20170220"
        compile group: "com.fasterxml.jackson.core", name: "jackson-core", version: "2.8.7"
        compile group: "com.fasterxml.jackson.core", name: "jackson-databind", version: "2.8.7"
        compile group: "com.fasterxml.jackson.core", name: "jackson-annotations", version: "2.8.7"
    }
}

blossom {
    def location = 'src/main/java/valandur/webapi/WebAPI.java'
    replaceToken '@version@', project.version, location
}

spongestart {
    eula true
    minecraft project.minecraftVersion
}

dependencies {
    compile project(":webapi-api")
    compile group: "org.reflections", name: "reflections", version: "0.9.10"
    compile group: "net.jodah", name: "typetools", version: "0.4.9"
    compile group: "org.mindrot", name: "jbcrypt", version: "0.4"
}

shadowJar {
    configurations  = [project.configurations.compile]

    relocate "com", "valandur.webapi.shadow.com"
    relocate "edu", "valandur.webapi.shadow.edu"
    relocate "javassist", "valandur.webapi.shadow.javassist"
    relocate "javax", "valandur.webapi.shadow.javax"
    relocate "org.mindrot", "valandur.webapi.shadow.org.mindrot"
    relocate "org.eclipse", "valandur.webapi.shadow.org.eclipse"
    relocate "org.reflections", "valandur.webapi.shadow.org.reflections"
    relocate "net", "valandur.webapi.shadow.net"

    exclude "/about.html"
    exclude "/jetty-dir.css"
    exclude "/valandur/webapi/api/WebAPIMod.class"

    archiveName = "webapi-${version}.jar"
}
shadowJar.dependsOn([':webapi-api:build'])

task copyJars(type: Copy, dependsOn: ':webapi-api:build') {
    from([subprojects.jar, shadowJar])
    into project.file('artifacts')
}

artifacts {
    archives shadowJar
}

build.dependsOn(shadowJar)
build.dependsOn(copyJars)
